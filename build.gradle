buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-compose'

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}
dockerCompose.isRequiredBy(test)

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compileOnly 'org.projectlombok:lombok'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'io.rest-assured:rest-assured:3.0.6'
	runtime 'org.postgresql:postgresql'
	runtime 'org.springframework.boot:spring-boot-devtools'
}

task waitForEventsAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/events', 'Events API'
}

task runEventsAPITests(type: Exec) {
	workingDir './events-api'
	commandLine './gradlew', 'test'
}

task allTests(type: Test) {
	dependsOn cleanTest
	dependsOn waitForEventsAPI
	dependsOn runEventsAPITests

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

test {
	dependsOn cleanTest
	dependsOn waitForEventsAPI

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

dockerCompose.isRequiredBy(allTests)
